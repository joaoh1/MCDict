plugins {
	id 'fabric-loom' version '0.5-SNAPSHOT'
	id 'maven-publish'
}

archivesBaseName = project.archives_base_name
group = project.maven_group

if (System.env.GITHUB_ACTIONS == "true") {
	version = "${project.mod_version}-github.${System.env.GITHUB_RUN_NUMBER}"
} else {
	version = project.mod_version
}

sourceSets {
	testmod {
		compileClasspath += main.compileClasspath
		runtimeClasspath += main.runtimeClasspath
	}
}

repositories {
	maven { url "https://server.bbkr.space/artifactory/libs-release" }
	mavenCentral()
}

dependencies {
	// to change the versions, see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	
	modImplementation "io.github.cottonmc:StaticData:${project.static_data_version}"
	//include "io.github.cottonmc:StaticData:${project.static_data_version}"

	testmodImplementation sourceSets.main.output
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

// ensure that the encoding is set to UTF-8 and that the build works on Java 8.
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
	options.release = 8
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this task, sources will not be generated.
	withSourcesJar()

	// if Java 8 is used, demand the next LTS release (Java 11)
	toolchain {
		if (JavaVersion.current().isJava8()) {
			languageVersion = JavaLanguageVersion.of(11)
		}
	}
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
	}
}